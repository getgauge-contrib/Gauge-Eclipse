grammar io.getgauge.Spec 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate spec "http://www.getgauge.io/Spec"

Model:
	definitions+=(Spec | Scenario | Step | Comment)*
;

// Two formats of Markdown H1
Spec:
	('# ' name+=TextPart+ SINGLE_NL+) | (name+=TextPart+ SINGLE_NL '='+ SINGLE_NL+)
;

// Two formats of Markdown H2
Scenario:
	('## ' name+=TextPart+ SINGLE_NL+) | (name+=TextPart+ SINGLE_NL '-'+ SINGLE_NL+)
;

// Markdown Unordered List - can contain parameters, Static (i.e. "foo") or Dynamic (i.e. <bar>)
// Can also optionally have a Table associated
Step:
	{Step}
	'* ' ((staticParams+=StaticParam | dynamicParams+=DynamicParam | WORD | '-' | '=' | WS) SEPARATORS*)+
	(SINGLE_NL SINGLE_NL* table=Table)?
	SINGLE_NL+
;

StaticParam:
	name=STATIC_PARAM
;

DynamicParam:
	name=DYNAMIC_PARAM
;

// Any text that is not a Spec, Scenario, Step or Tag
Comment:
	name+=TextPart name+=(TextPart | '-' | '=' | '|')+ SINGLE_NL+
;

TextPart:
	WORD | SEPARATORS | STATIC_PARAM | DYNAMIC_PARAM | WS
;

Table:
	WS* heading=TableRow
	WS* ('|' '-'+)+ TABLE_ROW_END
	WS* (rows += TableRow)+
;

TableRow:
	cells+=TableCell+ TABLE_ROW_END
;

TableCell:
	'|' name+=(WORD | SEPARATORS | WS)+
;

terminal SINGLE_NL: '\r'? '\n';

terminal WORD: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal STATIC_PARAM: '"' -> '"';

terminal DYNAMIC_PARAM: '<' -> '>';

terminal TABLE_ROW_END: '|' WS* SINGLE_NL;

terminal WS: ' ' | '\t';

terminal SEPARATORS: !('\r' | '\n' | 'a'..'z' | 'A'..'Z' | '0'..'9' | '_' | ' ' | '\t');
