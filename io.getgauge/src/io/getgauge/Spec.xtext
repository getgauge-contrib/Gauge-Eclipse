grammar io.getgauge.Spec 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate spec "http://www.getgauge.io/Spec"
/*
 * TODO
 * - Errors if last line is not a newline
 */

Model:
	{Model}
	SINGLE_NL*
	definitions+=Element*
;

Element:
	Spec | Scenario | Step | Comment | Tags
;

// Two formats of Markdown H1
Spec:
	('# ' name+=TextPart+ SINGLE_NL+) | (name+=TextPart+ SINGLE_NL '='+ SINGLE_NL+)
;

// Two formats of Markdown H2
Scenario:
	('## ' name+=TextPart+ SINGLE_NL+) | (name+=TextPart+ SINGLE_NL '-'+ SINGLE_NL+)
;

// Markdown Unordered List - can contain parameters, Static (i.e. "foo") or Dynamic (i.e. <bar>)
// Can also optionally have a Table associated
Step:
	{Step}
	definition=StepDefinition SINGLE_NL
	(SINGLE_NL* table=Table)?
	SINGLE_NL*
;

StepDefinition:
	{StepDefinition}
	'* ' ((staticParams+=StaticParam | dynamicParams+=DynamicParam | text+=(WORD | '-' | '=' | WS)) separators+=SEPARATORS*)+
;
StaticParam:
	name=STATIC_PARAM
;

DynamicParam:
	name=DYNAMIC_PARAM
;

Tags:
	{Tags}
	'tags' WS* ':' (WORD | WS |',')+ SINGLE_NL+  
;

// Any text that is not a Spec, Scenario, Step or Tag
Comment:
	name+=TextPart name+=(TextPart | '-' | '=' | '|' | ',' | ':')* SINGLE_NL+
;

TextPart:
	WORD | SEPARATORS | STATIC_PARAM | DYNAMIC_PARAM | WS
;

Table:
	WS* heading=TableRow
	WS* ('|' '-'+)+ TABLE_ROW_END
	(WS* rows += TableRow)+
;

TableRow:
	cells+=TableCell+ TABLE_ROW_END
;

TableCell:
	'|' name+=(WORD | SEPARATORS | WS)+
;

terminal SINGLE_NL: ('\r'? '\n');

terminal WORD: ('a'..'z'|'A'..'Z'|'_'|'0'..'9')+;

terminal STATIC_PARAM: '"' -> '"';

terminal DYNAMIC_PARAM: '<' -> '>';

terminal TABLE_ROW_END: '|' (SINGLE_NL | EOF);

terminal WS: ' ' | '\t';

terminal SEPARATORS: !('\r' | '\n' | 'a'..'z' | 'A'..'Z' | '0'..'9' | '_' | ' ' | '\t');
