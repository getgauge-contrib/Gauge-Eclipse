grammar io.getgauge.Spec 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate spec "http://www.getgauge.io/Spec"

Model:
	definitions+=AbstractEntity*
;

AbstractEntity:
	Spec | Scenario | Step | (Comment)
;

Spec:
	'# ' name+=(WORD | SEPARATORS)+ SINGLE_NL+
;

Scenario:
	'## ' name+=(WORD | SEPARATORS)+ SINGLE_NL+
;

Step:
	{Step}
	'* ' (((staticParams+=StaticParam) | (dynamicParams+=DynamicParam) | WORD) SEPARATORS*)+ SINGLE_NL+
;

terminal SINGLE_NL:
	'\r'? '\n'
;

terminal WORD: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal SEPARATORS:
	!('\r' | '\n' | 'a'..'z' | 'A'..'Z' | '0'..'9' | '_' ) 
;

StaticParam:
	value=STATIC_PARAM
;

DynamicParam:
	'<' type=('table'|'file') ':' value=DYNAMIC_PARAM_VALUE '>'
;

Comment:
	(text+=(WORD | SEPARATORS | STATIC_PARAM | DYNAMIC_PARAM_VALUE))+ SINGLE_NL+
;

terminal STATIC_PARAM:
	'"' -> '"'
;

terminal DYNAMIC_PARAM_VALUE:
	WORD '.' ('txt'|'csv')
;
